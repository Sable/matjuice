/*
 *  Copyright 2014-2015, Vincent Foley-Bourgon, McGill University
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

Program      ::= Function:Function*;
Function     ::= [FunctionName:FunctionName] Param:Parameter* StmtBlock:StmtBlock;
FunctionName ::= <Name:String>;
Parameter    ::= <Name:String>;


abstract Stmt;
StmtBlock:Stmt      ::= <Braces:Boolean> Stmt:Stmt*;
StmtExpr:Stmt       ::= Expr:Expr;
StmtReturn:Stmt     ::= [Expr:Expr];
StmtIfThenElse:Stmt ::= Cond:Expr Then:StmtBlock Else:StmtBlock;
StmtWhile:Stmt      ::= Cond:Expr Body:StmtBlock;
StmtFor:Stmt        ::= Init:Expr Test:Expr Update:Expr Body:StmtBlock;
StmtVarDecl:Stmt    ::= <Id:String> [Init:Expr];
StmtComment:Stmt    ::= <Text:String>;
StmtBreak:Stmt ;
StmtContinue:Stmt ;
StmtEmpty:Stmt ;


abstract Expr;
ExprInt:Expr         ::= <Value:int>;
ExprFloat:Expr       ::= <Value:double>;
ExprString:Expr      ::= <Value:String>;
ExprBoolean:Expr     ::= <Value:Boolean>;
ExprCall:Expr        ::= Expr:Expr Argument:Expr*;
ExprAssign:Expr      ::= LHS:Expr RHS:Expr;
ExprUnaryOp:Expr     ::= <Op:String> Expr:Expr;
ExprBinaryOp:Expr    ::= <Op:String> LHS:Expr RHS:Expr;
ExprId:Expr          ::= <Name:String>;
ExprArray:Expr       ::= Expr:Expr*;
ExprPropertyGet:Expr ::= Expr:Expr Property:Expr;
ExprColon:Expr ;
